apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'com.yuehai.android'
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    flavorDimensions 'environment', 'company'
    productFlavors {
        environmentDev {
            dimension 'environment'
            buildConfigField 'String', 'BASE_URL', '"http://10.10.33.131"'
        }
        environmentProd {
            dimension 'environment'
            buildConfigField 'String', 'BASE_URL', '"https://xxxx"'
        }
        companyA {
            dimension 'company'
            buildConfigField 'String', 'COMPANY_NAME', '"百度"'
        }
        companyB {
            dimension 'company'
            buildConfigField 'String', 'COMPANY_NAME', '"腾讯"'
        }
        companyC {
            dimension 'company'
            buildConfigField 'String', 'COMPANY_NAME', '"阿里巴巴"'
        }
    }
    signingConfigs {
        app_sample {
            keyAlias 'key'
            keyPassword '123456'
            storeFile file('app_sample.jks')
            storePassword '123456'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            // app_sample-版本名称-渠道名-编译时间.apk
            outputFileName = "app_sample-v${defaultConfig.versionName}-${buildTime()}.apk"
        }
    }
    buildTypes {
        release {
            minifyEnabled true//混淆是否开启
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.app_sample
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.app_sample
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':library_base')
    implementation project(':library_chart')
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-andx-4'
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
}

static def buildTime() {
    return new Date().format('yyyyMMddHHmm')
}
